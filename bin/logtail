#!/usr/bin/env perl 
# logtail
# Rémi Paulmier <remi.paulmier@gmail.com>
# read log files and inject messages into redis broker
# messages are encoded into JSON, in the same way logstash does
# This program needs inotify in linux kernel >= 2.6.13  
# greetly inspired by iwatch

use strict;
use Event;
use Sys::Hostname;
use Linux::Inotify2;
use File::Basename;
use XML::SimpleObject::LibXML;
use JSON;
use Redis 1.951; 
use DateTime;
use Log::Log4perl qw(:easy);
use Getopt::Std;
Log::Log4perl->easy_init($ERROR);

my %config;
my %states;
my %runtime;
my %options;
my $inotify;
my $logger;

my $PROGRAM = "logtail";
my $VERSION = "0.1.1";

use constant CONFDIR => "/etc/logtail";

sub get_event_tags {

	my $e = shift;
	my @tags;

	if ($e->IN_MODIFY) {
		push @tags, "IN_MODIFY";
	}
	if ($e->IN_ATTRIB) {
		push @tags, "IN_ATTRIB";
	}
	if ($e->IN_CLOSE_WRITE) {
		push @tags, "IN_CLOSE_WRITE";
	}
	if ($e->IN_CLOSE_NOWRITE) {
		push @tags, "IN_CLOSE_NOWRITE";
	}
	if ($e->IN_OPEN) {
		push @tags, "IN_OPEN";
	}
	if ($e->IN_MOVED_FROM) {
		push @tags, "IN_MOVED_FROM";
	}
	if ($e->IN_MOVED_TO) {
		push @tags, "IN_MOVED_TO";
	}
	if ($e->IN_CREATE) {
		push @tags, "IN_CREATE";
	}
	if ($e->IN_DELETE) {
		push @tags, "IN_DELETE";
	}
	if ($e->IN_DELETE_SELF) {
		push @tags, "IN_DELETE_SELF";
	}
	if ($e->IN_MOVE_SELF) {
		push @tags, "IN_MOVE_SELF";
	}
	if ($e->IN_ALL_EVENTS) {
		push @tags, "IN_ALL_EVENTS";
	}
	if ($e->IN_ONESHOT) {
		push @tags, "IN_ONESHOT";
	}
	if ($e->IN_ONLYDIR) {
		push @tags, "IN_ONLYDIR";
	}
	if ($e->IN_DONT_FOLLOW) {
		push @tags, "IN_DONT_FOLLOW";
	}
	if ($e->IN_MASK_ADD) {
		push @tags, "IN_MASK_ADD";
	}
	if ($e->IN_CLOSE) {
		push @tags, "IN_CLOSE";
	}
	if ($e->IN_MOVE) {
		push @tags, "IN_MOVE";
	}
	if ($e->IN_ISDIR) {
		push @tags, "IN_ISDIR";
	}
	if ($e->IN_Q_OVERFLOW) {
		push @tags, "IN_Q_OVERFLOW";
	}
	if ($e->IN_UNMOUNT) {
		push @tags, "IN_UNMOUNT";
	}
	if ($e->IN_IGNORED) {
		push @tags, "IN_IGNORED";
	}
	if ($e->IN_ONESHOT) {
		push @tags, "IN_ONESHOT";
	}

	return @tags;
}

sub message2logstash_event {
	my $vhost = shift;
	my $msg = shift;
	
	my $ltz = DateTime::TimeZone->new( name => 'UTC' );
	my $dt = DateTime->now(time_zone => $ltz);
	my %ls_event = ( '@timestamp' => $dt->iso8601(),
	                 '@type' => $config{vhosts}{$vhost}{type},
	                 '@message' => $msg,
	                 '@source_host' => $runtime{hostname},
	                 '@source_path' => $vhost,
	                 '@source' => 'file://' . $config{vhosts}{$vhost}{path}
	               );
	
	return encode_json \%ls_event;
}

sub redis_connect {
	if (!defined($runtime{redis})) {
		
		$logger->debug("first connecting to redis broker: " .
		               $config{broker}{host} . ":" .
		               $config{broker}{port}
		              );
		
		$runtime{redis} = Redis->new(server => $config{broker}{host}.':'.
		                             $config{broker}{port},
		                             reconnect => 60,
		                             every => 100,
		                             encoding => undef);
		$runtime{redis}->select($config{broker}{db});
	}
}

sub redis_output {
	my @msgs = @_;
	my $chunksize = 50;
	
	&redis_connect;
	
	my $redis = $runtime{redis};
	my $key = $config{broker}{key};

	# split @msgs in chunks of fixed number of elements
	$logger->debug("msgs is ".scalar(@msgs) ." long");
	while (scalar(@msgs) > $chunksize) {
		$redis->rpush($key, splice(@msgs, 0, $chunksize));
	}
	$redis->rpush($key, @msgs);
}

sub read_file {
	my $logfile = shift;
	my $buffer;
	
	# know the offset
	my $off = $states{'logfiles'}{$logfile}{'offset'};
	my ($dev,$ino,$mode,$nlink,$uid,$gid,$rdev,$size,
	    $atime,$mtime,$ctime,$blksize,$blocks)
	  = stat($logfile);
	$logger->debug("stat() $logfile: size is $size, mtime is ". 
	               scalar localtime $mtime );
	
	# something to read ?
	$logger->debug("size=$size, offset=$off");
	if ($size > $off) {
		open LF, "<$logfile" or die "can't open $logfile";
		seek LF, $off, 0 or die "can't seek $logfile";
		my ($tb, $nb) = (0, 0);
		while ( $tb < $size - $off) {
			$nb = read LF, $buffer, 1024*1024 or 
			  $logger->logdie("can't read from $logfile");
			$tb += $nb;
			$logger->debug("read $tb bytes");
		}
		close LF or die "can't close $logfile";
		$states{'logfiles'}{$logfile}{'offset'} += $tb;
	} else {
		$logger->debug("NOTHING TO READ");
	}
	
	# bulk send	
	my @msgs = split /\n/, $buffer;
	my @json_msgs = map {message2logstash_event($config{logfiles}
	                                            {$logfile}{vhost},
	                                            $_) } @msgs;
	if (scalar (@json_msgs) > 0) {
		redis_output(@json_msgs);
	}
}

sub watch_callback {
	my $e = shift;

	# event
	if ($logger->is_debug()) {
		$logger->debug("Event name: " . $e->name);
		$logger->debug("Event fullname: ". $e->fullname);
		$logger->debug("Event cookie: ". $e->cookie);
	}
		               
	# get event name
	my @tags = get_event_tags($e);
	if ($logger->is_debug()) {
		my $seq = int(rand(100));
		map { $logger->debug("$seq: $_"); } @tags
	}
	
	if ($e->IN_CREATE) {
		# an object has ben created in this watched dir
		if (exists $config{'logfiles'}{$e->fullname}) {
			$logger->debug("we want to watch this file");
			
			# first init its state
			$states{'logfiles'}{$e->fullname}{'offset'} = 0;
			
			# then consume it
			read_file($e->fullname);
			
			# then add a watcher on file
			if (!$inotify->watch ($e->fullname, 
			                      IN_DELETE_SELF,
			                      \&watch_callback)) {
				$logger->warn("can't add watcher on ".$e->fullname);
			}
		}
	}
	
	if ($e->IN_DELETE_SELF) {
		# this watched object has been removed
		if (exists $config{'logfiles'}{$e->fullname}) {
			$logger->debug("this file was supposed to be watched,"
			               . " cancel it");
			$e->w->cancel();
		}
	}
	
	if ($e->IN_MOVED_FROM) {
		# an object has been moved in this watched dir
		if (exists $config{'logfiles'}{$e->fullname}) {
			my $logfile = $e->fullname;
			my $vhost = $config{logfiles}{$e->fullname}{vhost};
			my $prev = $config{vhosts}{$vhost}{prev};
			
			# le fichier bougé est-il le précédent ?
			if ( $prev eq $logfile ) {
				# oui => on ne fait rien
			} else {
				# non, on initialise le state du prev
				# qui sera watché dans le MOVED_TO
				$states{logfiles}{$prev}{offset} = 
				  $states{logfiles}{$logfile}{offset};
			}
		}
	}
	
	if ($e->IN_MOVED_TO) {
		# an object has ben moved in this watched dir
		if (exists $config{'logfiles'}{$e->fullname}) {
			
			# first consume
			read_file($e->fullname);
			
			# then add a watcher on file
			if (!$inotify->watch ($e->fullname, 
			                      IN_DELETE_SELF,
			                      \&watch_callback)) {
				$logger->warn("can't add watcher on ".$e->fullname);
			}
		}
	}
}

# read conf from XML file
sub readXMLConf {
	my ($configFile) = @_;
	if(! -f $configFile) {
		$logger->logdie("can't read $configFile");
	}
	my $parser = new XML::LibXML;
	open(CF,"<$configFile");
	if(<CF> =~ /^<\?xml/) {
		$parser->validation(1);
	}
	else {
		$logger->logdie("Please use DTD for xml validation!");
		$parser->validation(0);
	}
	close(CF);
	my $xmlobj = new XML::SimpleObject::LibXML($parser->
	                                           parse_file("$configFile"));

	my $broker = $xmlobj->child("config")->child("broker");
	$config{broker}{host} = $broker->attribute("host");
	$config{broker}{port} = $broker->attribute("port");
	$config{broker}{db} = $broker->attribute("db");
	$config{broker}{key} = $broker->attribute("key");
	
	my $logfiles = $xmlobj->child("config")->child("logfiles");
	
	foreach my $logfile ($logfiles->children("logfile")) {
		my $path = $logfile->attribute("path");
		my $vhost = $logfile->attribute("vhost");
		my $prev = $logfile->attribute("prev");
		my $type = $logfile->attribute("type");
		
		$config{"vhosts"}{$vhost}{'path'} = $path;
		$config{"vhosts"}{$vhost}{'prev'} = $prev;
		$config{"vhosts"}{$vhost}{'type'} = $type;
		$logger->debug($vhost);
		$logger->debug("  ".$config{"vhosts"}{$vhost}{'path'});
		$logger->debug("  ".$config{"vhosts"}{$vhost}{'prev'});
		$logger->debug("  ".$config{"vhosts"}{$vhost}{'type'});

		$config{"logfiles"}{$path}{'vhost'} = $vhost;
		$config{"logfiles"}{$prev}{'vhost'} = $vhost;
	}
}

# init. states using default values
sub initStates {
	foreach my $logfile (keys %{$config{"logfiles"}}) {
		my ($ino, $size);
		
		(undef,
		 $ino,
		 undef,undef,undef,undef,undef,
		 $size,
		 undef, undef, undef, undef, undef) 
		  = stat($logfile) or sub {
			  $size = 0;
			  $ino = -1;
		  };
		
		$states{'logfiles'}{$logfile}{"inode"} = $ino;
		$states{'logfiles'}{$logfile}{"offset"} = $size;
	}
}

# read states from XML file
sub readXMLStates {
	
	my ($statesFile) = @_;
	if(! -f $statesFile) {
		$logger->warn("can't read $statesFile");
		return;
	}
	
	my $parser = new XML::LibXML;
	open(SF,"<$statesFile");
	if(<SF> =~ /^<\?xml/) {
		$parser->validation(1);
	}
	else {
		$logger->error("Please use DTD for xml validation!");
		$parser->validation(0);
	}
	close(SF);
	my $xmlobj = new XML::SimpleObject::LibXML($parser->
	                                           parse_file("$statesFile"));
	my $states = $xmlobj->child("states");
	
	foreach my $logfile ($states->children("logfile")) {
		my $path = $logfile->attribute("path");
		my $inode = $logfile->attribute("inode");
		my $offset = $logfile->attribute("offset");
		
		$states{"logfiles"}{$path}{'offset'} = $offset;
		$states{"logfiles"}{$path}{'inode'} = $inode;

		$logger->debug($path);
		$logger->debug("  ".$states{"logfiles"}{$path}{'offset'});
		$logger->debug("  ".$states{"logfiles"}{$path}{'inode'});
	}
}

# write states into XML file
sub writeXMLStates {

	my ($statesFile) = @_;

	$logger->debug("writing states to XML file");
	
	my $dom = XML::LibXML::Document->new('1.0');
	
	# set DTD <!DOCTYPE states SYSTEM "/etc/logtail/logtail-states.dtd" >
	$dom->createInternalSubset("states", undef,
	                           $runtime{confdir} . "/states.dtd");
	
	my $statesElement = $dom->createElement("states");
	$dom->addChild($statesElement);
	
	
	foreach my $logfile (keys %{$config{'logfiles'}}) {
		$logger->debug("writing state for $logfile");
		my $logElement = $dom->createElement("logfile");
		
		$logElement->addChild( $dom->createAttribute("path", $logfile) );
		$logElement->addChild( $dom->createAttribute("inode", 
		                                             $states{'logfiles'}
		                                             {$logfile}{'inode'}) );
		$logElement->addChild( $dom->createAttribute("offset", 
		                                             $states{'logfiles'}
		                                             {$logfile}{'offset'}) );
		$statesElement->addChild($logElement);
	}
	
	if (!$dom->is_valid()) {
		$logger->warn("document is not valid!");
	} else {
		# save
		open SF, ">$statesFile" or $logger->logdie("can't open $statesFile");
		binmode SF; 
		print SF $dom->toString();
		close SF;
	}
}

sub Usage {
	VERSION_MESSAGE();
	HELP_MESSAGE();
}

sub VERSION_MESSAGE {
	print <<ENDOFVERSION;

$PROGRAM $VERSION, a log shipper to redis broker, using logstash JSON format
(C) Remi Paulmier, Paris 2013.
ENDOFVERSION
}

sub HELP_MESSAGE {
	print <<ENDOFHELP;
  
$PROGRAM has following options:
Usage: $PROGRAM [-d <config dir>]
  -d <config dir>
     Specify an alternate configuration dir

ENDOFHELP
}

###################### STARTUP ##########################

my $opt = getopts("d:h",\%options);
if ($opt == 0 || defined($options{h})) {
	Usage();
	exit 1;
}

if(defined $options{d}) {
	$runtime{confdir} = $options{d};
} else {
	$runtime{confdir} = CONFDIR;
}
 
# logger init, always success
my $logconf = q(
log4perl.category.logtail		= ERROR, stderr

log4perl.appender.stderr		= Log::Log4perl::Appender::Screen
log4perl.appender.stderr.stderr = 1
log4perl.appender.stderr.layout	= Log::Log4perl::Layout::SimpleLayout
);
Log::Log4perl::init( \$logconf);
$logger = Log::Log4perl::get_logger('logtail');

# then,  tries to init log4perl from hardcoded conffile
Log::Log4perl::init( $runtime{confdir} . "/log4perl.conf") or 
  $logger->logdie("can't read log4perl conf");

# logger is OK, let's start !
$logger->warn("logtail v0.1 starting");

# read config
readXMLConf($runtime{confdir} . "/config.xml");

initStates();
readXMLStates($runtime{confdir} . "/states.xml");

# init hostname
$runtime{hostname} = hostname;

# init msgcount
$runtime{msgcount} = 0;

# add watched files
$inotify = new Linux::Inotify2;
foreach my $vhost (keys %{$config{'vhosts'}}) {
	if (!$inotify->watch ($config{vhosts}{$vhost}{path}, 
	                      IN_DELETE_SELF,
	                      \&watch_callback)) {
		$logger->warn("can't add watcher on ".$config{vhosts}
		              {$vhost}{path} );
	}

	if (!$inotify->watch ($config{vhosts}{$vhost}{prev}, 
	                      IN_DELETE_SELF,
	                      \&watch_callback)) {
		$logger->warn("can't add watcher on ".$config{'vhosts'}
		              {$vhost}{prev} );
	}
	
	# add a watcher on the container directory too
	my $dir = dirname($config{'vhosts'}{$vhost}{'path'});
	if (!$inotify->watch ($dir, 
	                      IN_CREATE | IN_MOVED_TO | IN_MOVED_FROM,
	                      \&watch_callback)) {
		$logger->warn("can't add watcher on ".$dir);
	}
}

# catch events from Inotify
Event->io (fd => $inotify->fileno, poll => 'r', cb => sub { $inotify->poll });

# catch some signals
Event->signal (signal => 'HUP',
               cb => sub {
	               $logger->warn("SIGHUP received");
	               while (my ($key, $watch) = each (%{$inotify->{'w'}})) {
		               $watch->cancel;
	               }
               });
Event->signal (signal => 'INT',
               cb => sub {
	               $logger->warn("SIGINT received");
	               while (my ($key, $watch) = each (%{$inotify->{'w'}})) {
		               $watch->cancel;
	               }
	               
	               Event::unloop_all(1);

	               $runtime{shouldstop} = 1;
               });
Event->signal (signal => 'TERM',
               cb => sub {
	               $logger->warn("SIGTERM received");
	               while (my ($key, $watch) = each (%{$inotify->{'w'}})) {
		               $watch->cancel;
	               }
	               
	               Event::unloop_all(1);

	               $runtime{shouldstop} = 1;
               });
# main loop
while (! defined($runtime{shouldstop})) {
	# first try to intercept file moves
	Event::loop(1);
	
	# then catch content of modified files
	foreach my $vhost (keys %{$config{'vhosts'}}) {
		# consume all the file if size has changed
		read_file($config{vhosts}{$vhost}{path});
	}

	# write states
	writeXMLStates($runtime{confdir} ."/states.xml");
}

exit(0);

